apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    Properties properties = new Properties()
    if (rootProject.file("keys.properties").exists()) {
        properties.load(rootProject.file("keys.properties").newDataInputStream())
    }

    defaultConfig {
        applicationId "com.example.weather"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [ googleMapsApiKey : properties.getProperty("googleMapsApiKey", "") ]

        resValue "string", "open_weather_map_api_key_1", properties.getProperty('openWeatherMapApiKey1')
        resValue "string", "open_weather_map_api_key_2", properties.getProperty('openWeatherMapApiKey2')
        resValue "string", "open_weather_map_api_key_3", properties.getProperty('openWeatherMapApiKey3')
    }

    signingConfigs {
        config {
            storeFile file(properties.getProperty("nameStoreFile"))
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    //rxjava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation "io.reactivex.rxjava3:rxjava:3.0.11"
    implementation 'com.jakewharton.rxrelay3:rxrelay:3.0.0'
    //support
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    //worker manager
    implementation 'androidx.work:work-runtime-ktx:2.5.0'
    implementation 'androidx.work:work-gcm:2.5.0'
    //location services
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    //google map
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    //orm
    implementation 'com.j256.ormlite:ormlite-android:5.2'
    //moshi-kotlin
    implementation 'com.squareup.moshi:moshi-kotlin:1.11.0'
    //retrofit
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    //dagger
    implementation 'com.google.dagger:dagger:2.30.1'
    kapt 'com.google.dagger:dagger-compiler:2.29.1'
}